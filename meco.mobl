application meco

import mobl::ui::generic
import meco_Res
import meco_Styles
load mobilecozona.html



var paros = true;
var chartbase = "http://charts.eco.hu/tozsde/";



screen root() {
		var a= true
		var s= "<div style=\"text-align:center;\"><iframe id=\"ajframe\" width=\"98%\" height=\"120\" src=\"http://services.eco.hu/bb/meco/mobilecozona.html\" frameborder=\"0\" scrolling=\"no\"></iframe></div>"
		var f= true
		script {Beall_init();}
		block(logo_Style) {
			image("meco_logo.gif")
		}
		item(Fooldal_Item_Style, onclick={ Reszvenylista("Részvények", false, Beall.all().one().csaktiv );}) { "Részvény árfolyamok" }
		item(Fooldal_Item_Style, onclick={ Reszvenylista("Kedvencek", true, Beall.all().one().csaktiv );}) { "Kedvenceim árfolyamai" }
		item(Fooldal_Item_Style, onclick={ Beallit(); }) { "Beállítások" }
		nl()
		html("<div style=\"font-size:8px;\"> Hirdetés:</div>")	
		html(s)
		nl()
		block(Fooldal_Footer_Style) {
			image("logo.gif", width = 76, height = 15, align = "left", onclick={openUrl("http://www.eco.hu");})
			html("<a href=\"http://www.eco.hu/impresszum\">Impresszum</a>")
		}
}
 
 
 
screen Reszvenylista(mit: String, csakfav: Bool, csakakt: Bool) {

	var adatok = RVd.all()
	
	var beall = Beall.all().one()
	var gentime= async(tolt(csakfav, csakakt))
	header(mit) {
		whenLoaded(gentime, loadingMessage="", style = tolt_Style) {
			backButton("Vissza")
			button(beall.gen_ido, onclick={ gentime = null; gentime = tolt(csakfav, csakakt); })
		}	
	} 
	block()	{
		list(a in adatok ) {
			when(a.paros==0) {
    			item(List_Item_Style_0, onclick={ Egyreszveny(a, gentime, csakfav, csakakt);}) { 
    				label(a.ticker,szinez_ticker(a.valtozas))
    				label(a.utolso, szinez_ar(a.valtozas))
    				label(a.valtozas, szinez_valt(a.valtozas))
    				image(chartbase + a.stockid + "_t.png", valign="middle")
      			}
      		}
      		when(a.paros==1) {
    			item(List_Item_Style_1, onclick={ Egyreszveny(a, gentime, csakfav, csakakt);}) { 
    				label(a.ticker,szinez_ticker(a.valtozas))
    				label(a.utolso, szinez_ar(a.valtozas))
    				label(a.valtozas, szinez_valt(a.valtozas))
    				image(chartbase + a.stockid + "_t.png", valign="middle")
      			}
      		}
		}
	}
}


screen Egyreszveny(a: RVd, gi: String, csakfav: Bool, csakakt: Bool) {
		header(a.ticker) {
    	whenLoaded(gi, loadingMessage="", style = tolt_Style) {
			backButton("Vissza")
			button(gi, onclick={ gi = null; gi = tolt(csakfav, csakakt); })
		}
    }
    block(graf_Style) {
    	image(chartbase + a.stockid + "_d1_lines.png", width = window.innerWidth)
    }
    item(List_Item_Style_0) {
    	label("Utolsó:", ER_Header_Style)
    	label(a.utolso, ER_Item_Style)
    }
    item(List_Item_Style_1) {
    	var s = a.valtozas.toString() + " (" + a.valtszaz.toString() + ")"
    	label("Változás:", ER_Header_Style)
    	label(s , ER_Item_Style)
    }
    item(List_Item_Style_0) {
    	label("Kötés idõpontja:", ER_Header_Style)
    	label(a.ido, ER_Item_Style)
    }
    item(List_Item_Style_1) {
    	label("Elõzõ záró:", ER_Header_Style)
    	label(a.elozozaro, ER_Item_Style)
    }
    item(List_Item_Style_0) {
    	label("Nyitó:", ER_Header_Style)
    	label(a.nyito, ER_Item_Style)
    }
    item(List_Item_Style_1) {
    	label("Forgalom (db):", ER_Header_Style)
    	label(a.napiosszes, ER_Item_Style)
    }
    item(List_Item_Style_0) {
    	label("Forgalom (M):", ER_Header_Style)
    	label( a.forgalom, ER_Item_Style)
    }
    item(List_Item_Style_1) {
    	var s = a.veteliaj.toString() + " x " + a.vetelidbszam.toString()
    	label("Vétel x db:", ER_Header_Style)
    	label(s, ER_Item_Style)
    }
    item(List_Item_Style_0) {
    	var s = a.eladasiaj.toString() + " x " + a.eladasidbszam.toString()
    	label("Eladás x db:", ER_Header_Style)
    	label(s, ER_Item_Style)
    }
    item(List_Item_Style_1) {
    	var s = a.min.toString() + " - " + a.max.toString()
    	label("Napi intervallum:", ER_Header_Style)
    	label(s, ER_Item_Style)
    }
}


screen Kedvallit(){
	var c = Tickerek.all().order("fav", false).order("ticker", true)
	var t: String = null
	
	header("Kedvencnek") {
		script {
		if(c.count()==0) {
			t = null;
			t = tolt(false, true);
		}
		else { t="valami";}	
	}
    	whenLoaded(t, loadingMessage="", style = tolt_Style){
    		 backButton("Vissza")
		}    
    }
    
    list(a in c){
    	item(onclick={ a.fav = !a.fav;}) {
    		checkBox(a.fav, label=a.ticker)
    	}
    } 
}


screen Beallit() {
	var b = Beall.all().one()
	var c = Tickerek.all().list()
	
	header("Beállítások") {
    	backButton("Vissza")
	}
	table {
      row {
        headerCol { "Csak aktívakat"}
        col { checkBox(b.csaktiv) }
      }

      row {
        headerCol { "Kedvencek: "}
        col { button("Kiválaszt", onclick={ Kedvallit(); }) }
      }
    }
}


function csikoz(p: String):Style {
	if(p==0) {return List_Item_Style_0;}
	else {return List_Item_Style_1;}	
}
 

function szinez_ticker(a: String): Style {
	if(a<0) {
		return FloatBalra_ticker_P; 
	}
	else {
		return FloatBalra_ticker_Z; 
	}
}

function szinez_ar(a: String): Style {
	if(a<0) {
		return FloatJobbra_ar_P; 
	}
	else {
		return FloatJobbra_ar_Z; 
	}
}

function szinez_valt(a: String): Style {
	if(a<0) {
		return FloatJobbra_valt_P; 
	}
	else {
		return FloatJobbra_valt_Z; 
	}
}
